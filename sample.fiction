# Set up known vocabulary
@vocabulary:
	verbs:
		@pick: pick | pick up | get | pluck
		@look: look | look at | look up | examine | observe
		@go: go | go to | walk | walk to
		@use: use
		@open: open
		@talk: talk to | ask
		@give: give
		@climb: climb | escalate
	articles: a | the
	prepositions: with | to
	messages:
		cantPick: I can't pick that
		cantTraverse: I can't go there
		cantDo: I can't do that
		didntUnderstand: I didn't understand that
		pickedUp: You picked up
		emptyInventory: You don't have anything with you right now
		changedLocation: You went to
	magicPhrases:
		exit: exit | exit game | quit | quit game
		goBack: go back
		whereAmI: where am I
		easterEgg: hello world | hello world!

# Starting location and message
@start:
	location: frontYard
	welcome: You stand in the front yard of an old house. You remember nothing.

fruits:
	shortDescription: some red fruits
	longDescription: Small round red fruits. I hope they are edible.
	visible: false
	pickable: true
	on @give to darkFigure:
		exit: As you extend your arm to give the fruit away, a smile appears beneath the eyes. A smile so bright that illuminates everything. Everything is white now. You won the game! 

brassKey:
	shortDescription: an old key
	longDescription: A large brass key full of dirt.
	visible: false
	pickable: true
	
treeMouth:
	shortDescription: a dark, mouth like cavity.
	longDescription: A dark, mouth like cavity at the bottom of the tree. It seems to have bits of bones and ashes inside, and between that mess, a brass key.
	visible: false
	on @look:
		brassKey.visible: true

tree:
	shortDescription: evil looking tree
	longDescription: The trunk looks as if a giant hand had twisted its very fabric in evil delight, its branches a dozen hands reaching out threateningly; or perhaps, desperately. It gives you the creeps. You notice a small cavity near the roots. Perhaps you're imagining too much, but it does look like a mouth.
	visible: false
	on @look:
		treeMouth.visible: true
		
frontGate:
	shortDescription: front gate
	longDescription: The front gate of the house, tall and made of iron emulating leafs and branches, and with chains holding it shut.
	on @climb:
		display: The gate is tall enough to prove itself dangerously hard to climb. I don't think that's a good idea.
		
frontYard:
	shortDescription: the front yard
	longDescription: The ground is covered with a thick carpet of long dead brown grass, split by a winding pebbled path that leads from the front gate to the house's main door. On one side of the path, an eerie looking tree looms over with a threatening look, its bony fingers trying to reach out. On the other side there are some bushes from which hang ripe red fruits, waiting to be plucked.
	traversable: true
	on @look:
		tree.visible: true
		fruits.visible: true
	connections:
		houseEntry: house | door | main door
		frontGate: gate | front gate
		tree: tree
		treeMouth: cavity | tree cavity | mouth | tree mouth
		brassKey: key | brass key
		fruits: fruit | fruits | berries | red fruit | red fruits | red berries

houseEntry:
	shortDescription: the house's main door entry
	longDescription: The 2 massive wooden doors are shut, its outward faces inlaid with intricate mirrored drawings of exquisite craft, although kissed by the unkind passage of time. The are three wooden steps that cry under every footstep, and a small canopy that used to protect from the rain, but now has evidently fed generations of termites.
	traversable: true
	connections:
		frontYard: yard | front yard | entrance
		mainHall: inside | house
		mainDoor: door | doors | massive door | massive doors | main door | main doors
		
mainHall:
	shortDescription: the dark interior of the house
	longDescription: The inside of the house is ink black. It takes you a few moments before the crawling stains of dark and darker form shapes. Everything around seems ruined long ago. In the middle of it all, a dark figure stands still looking at you.
	traversable: false
	connections:
		houseEntry: main door | outside
		darkFigure: figure | dark figure | shape | dark shape | man | dark man
	on @look:
		darkFigure.visible: true
		
darkFigure:
	shortDescription: a dark figure
	longDescription: The dark figure stands in absolute stillness. You can't notice any breath movement on the chest nor shifting its weight. The only thing that is any different is its two liquid eyes fixed on you.
	visible: false
	on @talk:
		exit: As you attempt to speak, your voice flows through the air but gets sucked in the blackness of the shape, a blackness which widens and begins to engulf the light around. There is no more sound or light, only black. 
		
mainDoor:
	shortDescription: The main door
	longDescription: The massive wooden doors are locked shut
	on @open:
		display: The door is locked.
	on @open with key:
		mainHall.traversable: true
		display: You opened the door of the house. The doors open with a deep sound that echo inside.
		on @open:
			display: The door is already open.
		on @open with key:
			display: The door is already open.